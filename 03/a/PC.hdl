// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // store new value?
    Or(a=load, b=inc, out=loadinc1);
    Or(a=loadinc1, b=reset, out=loadinc);

    // Increment
    Inc16(in=regout, out=reginc);
    Mux16(a=regout, b=reginc, sel=inc, out=regout1);

    // Load
    Mux16(a=regout1, b=in, sel=load, out=regout2);
    
    // Reset
    Mux16(a=regout2, b=false, sel=reset, out=regout3);

    // Save (if load/reset or inc was 1) 
    Register(in=regout3, load=loadinc, out=regout, out=out);
}

